buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3'
        classpath 'pl.allegro.tech.build:axion-release-plugin:1.2.3'
        classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3'
        classpath 'info.solidsoft.gradle:deliveryboy:0.1.0-SNAPSHOT'
    }
}

apply plugin: 'groovy'
apply plugin: 'pl.allegro.tech.build.axion-release'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'io.codearte.nexus-staging'
apply plugin: 'info.solidsoft.deliveryboy'

deliveryBoy {
    ciType = "travis"
    releaseBranch = "deliveryBoy"
    dryRun = true
}

task fakeBuild << {}
task fakeUploadArchives << {}
task fakeCreateRelease << {}
task fakeCloseRepository << {}
task fakePushRelease << {}
task fakePromoteRepository << {}

//Move to plugin
currentVersion.mustRunAfter fakeCreateRelease
fakeCloseRepository.mustRunAfter fakeUploadArchives
fakePromoteRepository.mustRunAfter fakeUploadArchives, fakePushRelease
fakePushRelease.mustRunAfter fakeCloseRepository


scmVersion {
    tag {
        prefix = 'release'
        versionSeparator = '/'
    }
    createReleaseCommit = true
    releaseCommitMessage { version, position -> "Release version: ${version}\n\n[ci skip]" }
    hooks {
        pre 'fileUpdate', [file: 'README.md', pattern: {v, p -> /"info\.solidsoft\.gradle:gradle-template:.*"/},
                           replacement: {v, p -> "\"info.solidsoft.gradle:gradle-template:$v\""}]
    }
}

project.group = "info.solidsoft.gradle"
project.version = scmVersion.version

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.4'

    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile 'junit:junit:4.12'
}

nexusStaging {
    packageGroup = "info.solidsoft"
    stagingProfileId = "34a3a5beeaa96"
}

modifyPom {
    project {
        name 'Gradle template'
        description 'Gradle template'
        url 'https://github.com/szpak/dummy-cd-project'
        inceptionYear '2015'

        scm {
            url 'https://github.com/szpak/dummy-cd-project'
            connection 'scm:https://github.com/szpak/dummy-cd-project'
            developerConnection 'scm:git://github.com/szpak/dummy-cd-project'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'szpak'
                name 'Marcin ZajÄ…czkowski'
                email 'mszpak ATT wp DOTT pl'
                roles {
                    role 'developer'
                    role 'despot'
                }
            }
        }
    }
}

uploadArchives.dependsOn { check }
